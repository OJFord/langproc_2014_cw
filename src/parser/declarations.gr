//
//  declarations.gr
//  CARM-Compiler
//
//  Created by Ollie Ford on 01/03/2015.
//  Copyright (c) 2015 OJFord. All rights reserved.
//

declaration:
	declaration_specifiers
|
	declaration_specifiers initialiser_declarator_list
;

declaration_specifiers:
	storage_class_specifier
|
	storage_class_specifier declaration_specifiers
|
	type_specifier
|
	type_specifier declaration_specifiers
|
	type_qualifier
|
	type_qualifier declaration_specifiers
;

initialiser_declarator_list:
	initialiser_declarator
|
	initialiser_declarator_list COMMA initialiser_declarator
;

initialiser_declarator:
	declaration
|
	declaration EQUALS initialiser
;

storage_class_specifier:
	KW_TYPEDEF
|
	KW_EXTERN
|
	KW_STATIC
|
	KW_AUTO
|
	KW_REGISTER
;

type_specifier:
	KW_VOID
|
	KW_CHAR
|
	KW_SHORT
|
	KW_INT
|
	KW_LONG
|
	KW_FLOAT
|
	KW_DOUBLE
|
	KW_SIGNED
|
	KW_UNSIGNED
|
	struct_union_specifier
|
	enum_specifier
|
	typedef_name
;

struct_union_specifier:
	struct_union LBRACE struct_declaration_list RBRACE
|
	struct_union IDENTIFIER LBRACE struct_declaration_list RBRACE
|
	struct_union IDENTIFIER
;

struct_union:
	KW_STRUCT
|
	KW_UNION
;

struct_declaration_list:
	struct_declaration
|
	struct_declaration_list struct_declaration
;

struct_declaration:
	spec_qualifier_list struct_declarator_list SCOLON
;

spec_qualifier_list:
	type_specifier
|
	type_specifier spec_qualifier_list
|
	type_qualifier
|
	type_qualifier spec_qualifier_list
;

struct_declarator_list:
	struct_declarator
|
	struct_declarator_list COMMA struct_declarator
;

struct_declarator:
	declarator
|
	COLON constant_expr
|
	declarator COLON constant_expr
;

enum_specifier:
	KW_ENUM LBRACE enumerator_list RBRACE
|
	KW_ENUM IDENTIFIER LBRACE enumerator_list RBRACE
|
	KW_ENUM IDENTIFIER
;

enumerator_list:
	enumerator
|
	enumerator_list COMMA enumerator
;

enumerator:
	enumeration_constant
|
	enumeration_constant EQUALS constant_expr
;

type_qualifier:
	KW_CONST
|
	KW_VOLATILE
;

declarator:
	direct_declarator
|
	pointer direct_declarator
;

direct_declarator:
	IDENTIFIER
|
	LBRACKET declarator RBRACKET
|
	direct_declarator LSQUBRACKET RSQUBRACKET
|
	direct_declarator LSQUBRACKET constant_expr RSQUBRACKET
|
	direct_declarator LBRACKET param_type_list RBRACKET
|
	direct_declarator LBRACKET RBRACKET
|
	direct_declarator LBRACKET identifier_list RBRACKET
;

pointer:
	ASTERISK
|
	ASTERISK type_qualifier_list
|
	ASTERISK pointer
|
	ASTERISK type_qualifier_list pointer
;

type_qualifier_list:
	type_qualifier
|
	type_qualifier_list type_qualifier
;

param_type_list:
	param_list
|
	param_list COMMA PUNCTUATOR_TRIPLEDOT
;

param_list:
	param_declaration
|
	param_list COMMA param_declaration
;

param_declaration:
	declaration_specifiers declarator
|
	declaration_specifiers
|
	declaration_specifiers abstract_declarator
;

identifier_list:
	IDENTIFIER
|
	identifier_list COMMA IDENTIFIER
;

type_name:
	spec_qualifier_list
|
	spec_qualifier_list abstract_declarator
;

abstract_declarator:
	pointer
|
	direct_abstract_declarator
|
	pointer direct_abstract_declarator
;

direct_abstract_declarator:
	LBRACKET abstract_declarator RBRACKET
|
	LSQUBRACKET RSQUBRACKET
|
	direct_abstract_declarator LSQUBRACKET RSQUBRACKET
|
	LSQUBRACKET constant_expr RSQUBRACKET
|
	direct_abstract_declarator LSQUBRACKET constant_expr RSQUBRACKET
|
	LBRACKET RBRACKET
|
	direct_abstract_declarator LBRACKET RBRACKET
|
	LBRACKET param_type_list RBRACKET
|
	direct_abstract_declarator LBRACKET param_type_list RBRACKET
;

typedef_name:
	IDENTIFIER
;

initialiser:
	assignment_expr
|
	LBRACE initialiser_list RBRACE
|
	LBRACE initialiser_list COMMA RBRACE
;

initialiser_list:
	initialiser
|
	initialiser_list COMMA initialiser
;
