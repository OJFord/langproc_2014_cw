//
//  statements.gr
//  CARM_Compiler
//
//  Created by Ollie Ford on 01/03/2015.
//  Copyright (c) 2015 OJFord. All rights reserved.
//

stmt:
	labeled_stmt
|
	compound_stmt
|
	expr_stmt
|
	select_stmt
|
	iter_stmt
|
	jump_stmt
;

labeled_stmt:
	IDENTIFIER COLON stmt
|
	KW_CASE constant_expr COLON stmt
|
	KW_DEFAULT COLON stmt
;

compound_stmt:
	LBRACE RBRACE
|
	LBRACE declaration_list RBRACE
|
	LBRACE stmt_list RBRACE
|
	LBRACE declaration_list stmt_list RBRACE
;

declaration_list:
	declaration
|
	declaration_list declaration
;

stmt_list:
	stmt
|
	stmt_list stmt
;

expr_stmt:
	SCOLON
|
	expr SCOLON
;

select_stmt:
	KW_IF LBRACKET expr RBRACKET stmt
|
	KW_IF LBRACKET expr RBRACKET stmt KW_ELSE stmt
|
	KW_SWITCH LBRACKET expr RBRACKET stmt
;

iter_stmt:
	KW_WHILE LBRACKET expr RBRACKET stmt
|
	KW_DO stmt KW_WHILE LBRACKET expr RBRACKET SCOLON
|
	KW_FOR LBRACKET SCOLON SCOLON RBRACKET stmt
|
	KW_FOR LBRACKET expr SCOLON SCOLON RBRACKET stmt
|
	KW_FOR LBRACKET SCOLON expr SCOLON RBRACKET stmt
|
	KW_FOR LBRACKET SCOLON SCOLON expr RBRACKET stmt
|
	KW_FOR LBRACKET expr SCOLON expr SCOLON RBRACKET stmt
|
	KW_FOR LBRACKET expr SCOLON SCOLON expr RBRACKET stmt
|
	KW_FOR LBRACKET SCOLON expr SCOLON expr RBRACKET stmt
|
	KW_FOR LBRACKET expr SCOLON expr SCOLON expr RBRACKET stmt
;

jump_stmt:
	KW_GOTO IDENTIFIER SCOLON
|
	KW_CONTINUE SCOLON
|
	KW_BREAK SCOLON
|
	KW_RETURN SCOLON
|
	KW_RETURN expr SCOLON
;
